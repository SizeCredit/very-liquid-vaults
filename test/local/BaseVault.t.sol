// SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import {IAccessControl} from "@openzeppelin/contracts/access/IAccessControl.sol";
import {Initializable} from "@openzeppelin/contracts/proxy/utils/Initializable.sol";
import {UUPSUpgradeable} from "@openzeppelin-upgradeable/contracts/proxy/utils/UUPSUpgradeable.sol";
import {BaseTest} from "@test/BaseTest.t.sol";
import {BaseVault} from "@src/BaseVault.sol";
import {BaseVaultMock} from "@test/mocks/BaseVaultMock.t.sol";
import {PAUSER_ROLE, DEFAULT_ADMIN_ROLE, STRATEGIST_ROLE} from "@src/Auth.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {PausableUpgradeable} from "@openzeppelin-upgradeable/contracts/utils/PausableUpgradeable.sol";
import {ERC4626Upgradeable} from "@openzeppelin-upgradeable/contracts/token/ERC20/extensions/ERC4626Upgradeable.sol";

contract BaseVaultTest is BaseTest {
    function test_BaseVault_initialize() public view {
        assertEq(baseVault.asset(), address(erc20Asset));
        assertEq(baseVault.name(), "Base USD Coin Mock");
        assertEq(baseVault.symbol(), "baseUSDCMOCK");
        assertEq(baseVault.decimals(), erc20Asset.decimals(), 6);
    }

    function test_BaseVault_upgrade() public {
        BaseVaultMock newBaseVault = new BaseVaultMock();
        vm.prank(alice);
        vm.expectRevert(
            abi.encodeWithSelector(IAccessControl.AccessControlUnauthorizedAccount.selector, alice, DEFAULT_ADMIN_ROLE)
        );
        UUPSUpgradeable(address(baseVault)).upgradeToAndCall(address(newBaseVault), "");

        vm.prank(admin);
        UUPSUpgradeable(address(baseVault)).upgradeToAndCall(address(newBaseVault), "");
    }

    function test_BaseVault_initialize_invalidInitialization_reverts() public {
        BaseVaultMock newBaseVault = new BaseVaultMock();
        vm.expectRevert(Initializable.InvalidInitialization.selector);
        newBaseVault.initialize(auth, IERC20(address(0)), "Test", "TST", FIRST_DEPOSIT_AMOUNT);
    }

    function test_BaseVault_pause_success() public {
        assertFalse(baseVault.paused());

        vm.prank(admin);
        auth.grantRole(PAUSER_ROLE, admin);

        vm.prank(admin);
        baseVault.pause();

        assertTrue(baseVault.paused());
    }

    function test_BaseVault_pause_unauthorized_reverts() public {
        vm.prank(alice);
        vm.expectRevert(
            abi.encodeWithSelector(IAccessControl.AccessControlUnauthorizedAccount.selector, alice, PAUSER_ROLE)
        );
        baseVault.pause();
    }

    function test_BaseVault_unpause_success() public {
        vm.prank(admin);
        auth.grantRole(PAUSER_ROLE, admin);

        vm.prank(admin);
        baseVault.pause();
        assertTrue(baseVault.paused());

        vm.prank(admin);
        baseVault.unpause();

        assertFalse(baseVault.paused());
    }

    function test_BaseVault_unpause_unauthorized_reverts() public {
        vm.prank(admin);
        auth.grantRole(PAUSER_ROLE, admin);

        vm.prank(admin);
        baseVault.pause();

        vm.prank(alice);
        vm.expectRevert(
            abi.encodeWithSelector(IAccessControl.AccessControlUnauthorizedAccount.selector, alice, PAUSER_ROLE)
        );
        baseVault.unpause();
    }

    function test_BaseVault_deposit_whenPaused_reverts() public {
        uint256 amount = 100e6;
        _mint(erc20Asset, alice, amount);
        _approve(alice, erc20Asset, address(baseVault), amount);

        vm.prank(admin);
        auth.grantRole(PAUSER_ROLE, admin);

        vm.prank(admin);
        baseVault.pause();

        vm.prank(alice);
        vm.expectRevert(abi.encodeWithSelector(PausableUpgradeable.EnforcedPause.selector));
        baseVault.deposit(amount, alice);
    }

    function test_BaseVault_deposit_whenAuthPaused_reverts() public {
        uint256 amount = 100e6;
        _mint(erc20Asset, alice, amount);
        _approve(alice, erc20Asset, address(baseVault), amount);

        vm.prank(admin);
        auth.pause();

        vm.prank(alice);
        vm.expectRevert(abi.encodeWithSelector(PausableUpgradeable.EnforcedPause.selector));
        baseVault.deposit(amount, alice);

        vm.prank(admin);
        auth.unpause();

        vm.prank(alice);
        baseVault.deposit(amount, alice);
        assertEq(baseVault.balanceOf(alice), amount);
    }

    function test_BaseVault_transfer_whenPaused_reverts() public {
        uint256 amount = 100e6;
        _mint(erc20Asset, alice, amount);
        _approve(alice, erc20Asset, address(baseVault), amount);

        vm.prank(alice);
        baseVault.deposit(amount, alice);

        vm.prank(admin);
        auth.grantRole(PAUSER_ROLE, admin);

        vm.prank(admin);
        baseVault.pause();

        vm.prank(alice);
        vm.expectRevert(abi.encodeWithSelector(PausableUpgradeable.EnforcedPause.selector));
        baseVault.transfer(bob, amount);
    }

    function test_BaseVault_transfer_whenAuthPaused_reverts() public {
        uint256 amount = 100e6;
        _mint(erc20Asset, alice, amount);
        _approve(alice, erc20Asset, address(baseVault), amount);

        vm.prank(alice);
        baseVault.deposit(amount, alice);

        vm.prank(admin);
        auth.pause();

        vm.prank(alice);
        vm.expectRevert(abi.encodeWithSelector(PausableUpgradeable.EnforcedPause.selector));
        baseVault.transfer(bob, amount);
    }

    function test_BaseVault_deposit_withdraw_basic() public {
        uint256 depositAmount = 100e6;
        _mint(erc20Asset, alice, depositAmount);
        _approve(alice, erc20Asset, address(baseVault), depositAmount);

        vm.prank(alice);
        baseVault.deposit(depositAmount, alice);

        assertEq(baseVault.balanceOf(alice), depositAmount);
        assertEq(baseVault.totalAssets(), FIRST_DEPOSIT_AMOUNT + depositAmount);
        assertEq(erc20Asset.balanceOf(address(baseVault)), FIRST_DEPOSIT_AMOUNT + depositAmount);
        assertEq(erc20Asset.balanceOf(alice), 0);

        uint256 withdrawAmount = 30e6;
        vm.prank(alice);
        baseVault.withdraw(withdrawAmount, alice, alice);

        assertEq(baseVault.balanceOf(alice), depositAmount - withdrawAmount);
        assertEq(baseVault.totalAssets(), FIRST_DEPOSIT_AMOUNT + depositAmount - withdrawAmount);
        assertEq(erc20Asset.balanceOf(address(baseVault)), FIRST_DEPOSIT_AMOUNT + depositAmount - withdrawAmount);
        assertEq(erc20Asset.balanceOf(alice), withdrawAmount);
    }

    function test_BaseVault_setTotalAssetsCap() public {
        uint256 totalAssetsCap = 1000e6;
        vm.prank(alice);
        vm.expectRevert(
            abi.encodeWithSelector(IAccessControl.AccessControlUnauthorizedAccount.selector, alice, STRATEGIST_ROLE)
        );
        baseVault.setTotalAssetsCap(totalAssetsCap);

        assertEq(baseVault.totalAssetsCap(), type(uint256).max);

        vm.prank(strategist);
        baseVault.setTotalAssetsCap(totalAssetsCap);
        assertEq(baseVault.totalAssetsCap(), totalAssetsCap);
    }

    function test_BaseVault_deposit_reverts_when_totalAssetsCap_is_reached() public {
        uint256 totalAssetsCap = 1000e6;
        vm.prank(strategist);
        baseVault.setTotalAssetsCap(totalAssetsCap);

        _mint(erc20Asset, address(baseVault), totalAssetsCap);

        uint256 depositAmount = 100e6;
        _mint(erc20Asset, alice, depositAmount);
        _approve(alice, erc20Asset, address(baseVault), depositAmount);

        vm.prank(alice);
        vm.expectRevert(
            abi.encodeWithSelector(ERC4626Upgradeable.ERC4626ExceededMaxDeposit.selector, alice, depositAmount, 0)
        );
        baseVault.deposit(depositAmount, alice);
    }
}
